/*
 * This file is part of the Salt Edge Authenticator distribution
 * (https://github.com/saltedge/sca-authenticator-android).
 * Copyright (c) 2019 Salt Edge Inc.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 3 or later.
 *
 * This program is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 *
 * For the additional permissions granted for Salt Edge Authenticator
 * under Section 7 of the GNU General Public License see THIRD_PARTY_NOTICES.md
 */
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'idea'
apply plugin: 'realm-android'
apply from: './jacoco.gradle'
apply from: "./common-methods.gradle"

android {

    signingConfigs {
        release
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/com.squareup.retrofit/retrofit/pom.properties'
        exclude 'META-INF/maven/com.squareup.retrofit/retrofit/pom.xml'
    }

    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }

    defaultConfig {
        applicationId 'com.saltedge.authenticator'
        testApplicationId "com.saltedge.authenticator.tests"

        multiDexEnabled = true
        vectorDrawables.useSupportLibrary = true

        buildToolsVersion '28.0.3'
        minSdkVersion 23
        compileSdkVersion 28
        targetSdkVersion 28

        versionCode 32
        versionName '2.0.0-rc8'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            lintOptions {
                disable 'MissingTranslation'
                // google services plugin is causing missing translation issue
            }
            testCoverageEnabled false
            debuggable false
        }
        debug {
            lintOptions {
                disable 'MissingTranslation'
                // google services plugin is causing missing translation issue
            }
            testCoverageEnabled true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:28.0.0'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    kapt 'com.google.dagger:dagger-compiler:2.22.1'
    implementation 'com.google.dagger:dagger:2.22.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.android.gms:play-services-base:17.1.0'
    implementation 'com.google.android.gms:play-services-identity:17.0.0'
    implementation 'com.google.android.gms:play-services-analytics:17.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.firebase:firebase-core:17.1.0'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'net.danlew:android.joda:2.10.1.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.9') {
        transitive = true
    }
    implementation('com.github.bumptech.glide:glide:4.9.0') {
        exclude group: "com.android.support"
    }
    implementation 'com.google.android.gms:play-services-vision:18.0.0'
    implementation project(':authenticator_sdk')
    implementation project(':rasp')

    androidTestUtil 'com.android.support.test:orchestrator:1.0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'org.robolectric:shadows-playservices:4.3'
    testImplementation 'org.mockito:mockito-core:2.16.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-core:1.3'
    androidTestImplementation 'org.hamcrest:hamcrest-integration:1.3'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'org.mockito:mockito-android:2.16.0'
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.2'//for compatibility with last realm plugin version
}

idea {
    module {
        testOutputDir = file('build/test-classes/debug')
    }
}

repositories {
    mavenCentral()
    google()
    maven { url 'https://maven.fabric.io/public' }
}

setupSigning("./signing.properties", "release")

apply plugin: 'com.google.gms.google-services'
