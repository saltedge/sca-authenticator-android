/*
 * This file is part of the Salt Edge Authenticator distribution
 * (https://github.com/saltedge/sca-authenticator-android).
 * Copyright (c) 2020 Salt Edge Inc.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 3 or later.
 *
 * This program is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 *
 * For the additional permissions granted for Salt Edge Authenticator
 * under Section 7 of the GNU General Public License see THIRD_PARTY_NOTICES.md
 */
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'idea'
apply plugin: 'realm-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.firebase.crashlytics'
apply from: './jacoco.gradle'
apply from: './common-methods.gradle'

android {

    signingConfigs {
        release
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/com.squareup.retrofit/retrofit/pom.properties'
        exclude 'META-INF/maven/com.squareup.retrofit/retrofit/pom.xml'
        exclude "DebugProbesKt.bin"//Avoiding including the debug infrastructure of coroutines in the resulting APK
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        applicationId 'com.saltedge.authenticator'
        testApplicationId 'com.saltedge.authenticator.tests'

        multiDexEnabled = true
        vectorDrawables.useSupportLibrary = true

        buildToolsVersion '30.0.3'
        minSdkVersion 21
        compileSdkVersion 30
        targetSdkVersion 30

        versionCode 50
        versionName '3.3.0'

        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            lintOptions {
                disable 'MissingTranslation'
                // google services plugin is causing missing translation issue
            }
            testCoverageEnabled false
            debuggable false
        }
        debug {
            lintOptions {
                disable 'MissingTranslation'
                // google services plugin is causing missing translation issue
            }
            testCoverageEnabled true
        }
    }

    buildFeatures{
        dataBinding = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:29.0.0'
    }
}

androidExtensions {
    experimental = true
}

dependencies { //TODO After changing dependencies list, review third-party libraries licenses list
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    kapt 'com.google.dagger:dagger-compiler:2.28.3'
    implementation 'com.google.dagger:dagger:2.28.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.android.gms:play-services-base:17.6.0'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.firebase:firebase-core:18.0.3'
    implementation 'com.google.firebase:firebase-messaging:21.1.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.4.1'
    implementation 'com.google.firebase:firebase-analytics:18.0.3'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'net.danlew:android.joda:2.10.2'
    implementation('com.github.bumptech.glide:glide:4.11.0') {
        exclude group: "com.android.support"
    }
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.fivehundredpx:blurringview:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'
    implementation project(':authenticator_sdk_v1')
    implementation project(':authenticator_sdk_v2')
    implementation project(':rasp')

    androidTestUtil 'com.android.support.test:orchestrator:1.0.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'org.robolectric:shadows-playservices:4.3'
    testImplementation 'org.mockito:mockito-core:3.6.28'
    testImplementation "io.mockk:mockk:1.10.2"
    testImplementation project(':test_tools')

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'org.hamcrest:hamcrest-core:1.3'
    androidTestImplementation 'org.hamcrest:hamcrest-integration:1.3'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'org.mockito:mockito-android:3.2.4'
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.2'//for compatibility with last realm plugin version
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
}

idea {
    module {
        testOutputDir = file('build/test-classes/debug')
    }
}

repositories {
    mavenCentral()
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://github.com/500px/500px-android-blur/raw/master/releases/' }
}

setupSigning("./signing.properties", "release")

apply plugin: 'com.google.gms.google-services'
